# -*- mode: python; -*-
# This Python script, SConscript, invoked by the SConscript in the upper directory,
#
import os
import sys
import shutil


Import('env')
report_env = env.Clone()
report_env[ "REPORT_DIR" ] = 'lib_customreport_TBHIV_ReportByAge'

lib_report_dllSrcFiles = [ "$REPORT_DIR/Report_TBHIV_ByAge.cpp", "$REPORT_DIR/stdafx.cpp" ]
lib_report_dllIncFiles = [ "$REPORT_DIR/Report_TBHIV_ByAge.h" ]
lib_report_dllLocalInc = [ "$REPORT_DIR/stdafx.h" ]

report_env.Append( CPPDEFINES=["ENABLE_TBHIV" ] )

report_env.Append(LIBS=['baseReportLib'])
report_env.Append(LIBS=['utils'])
report_env.Append(LIBPATH=['$BUILD_DIR/baseReportLib'])
report_env.Append(LIBPATH=['$BUILD_DIR/utils'])

report_dir = report_env[ "BUILD_DIR" ] + 'reporter_plugins/' + report_env[ "REPORT_DIR" ]

lib_report_Dll = report_env.SharedLibrary( report_dir, lib_report_dllSrcFiles )
#print "building lib_report_dll: " + str(lib_report_Dll)

# take lib_report.dll (exclude .lib, .exp) out to be the building target
# to match the number of building variant
lib_report_dllTarget = lib_report_Dll[0]

msvc = GetOption( "MSVC" )
if msvc is not None and msvc != False and msvc != "":

    bvar = report_env['BUILD_VARIANT'] + '|x64'
    print( str(lib_report_dllTarget) + ": Generating MSVS" + report_env['MSVS_VERSION'] + " project files for '%s'" % bvar )
    
    report_env.MSVSProject(target = 'Lib_report_dllScons' + report_env['MSVSPROJECTSUFFIX'],
                    srcs = lib_report_dllSrcFiles,
                    incs = lib_report_dllIncFiles,
                    localincs = lib_report_dllLocalInc,
                    buildtarget = lib_report_dllTarget,
                    auto_build_solution = 0,
                    variant = bvar)
